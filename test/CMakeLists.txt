# Want test modules in the build directory.
#unset(CMAKE_Fortran_MODULE_DIRECTORY)

macro(add_parallel_test test exe nproc)
  add_executable(${exe} ${exe}.F90)
  target_link_libraries(${exe} common)
  if(USE_CAF)
    add_test(${test} ./${exe})
    if (CMAKE_Fortran_COMPILER_ID MATCHES NAG)
      set_tests_properties(${test} PROPERTIES ENVIRONMENT NAGFORTRAN_NUM_IMAGES=${nproc})
    elseif(CMAKE_Fortran_COMPILER_ID MATCHES Intel)
      set_tests_properties(${test} PROPERTIES ENVIRONMENT FOR_COARRAY_NUM_IMAGES=${nproc})
    endif()
    # Don't work?
    #set_tests_properties(${test} PROPERTIES ENVIRONMENT $<$<Fortran_COMPILER_ID:NAG>:NAGFORTRAN_NUM_IMAGES=${nproc}>)
    #set_tests_properties(${test} PROPERTIES ENVIRONMENT $<$<Fortran_COMPILER_ID:Intel>:FOR_COARRAY_NUM_IMAGES=${nproc}>)
  else()
    add_test(${test} ${MPIEXEC} ${MPIEXEC_NUMPROC_FLAG} ${nproc} ./${exe} ${MPIEXEC_POSTFLAGS})
  endif()
  set_tests_properties(${test} PROPERTIES PROCESSORS ${nproc})
endmacro()

add_parallel_test(localize localize_test 4)
add_parallel_test(gather gather_test 4)
add_parallel_test(scatter scatter_test 4)
add_parallel_test(collate collate_test 4)
add_parallel_test(distribute distribute_test 4)
